// Рагозина 09122023 <--

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных
	Перем ВидДоговора, НачалоДействияДоговора,ОкончаниеДействияДоговора, ЧасРаботы;
#КонецОбласти

#Область ОбработчикиСобытий

	Процедура ОбработкаПроведения(Отказ, Режим)
		
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор,"ВидДоговора");
		Если не ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			ОбщегоНазначения.СообщитьПользователю("Договор не подходит для обслуживания!");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		НачалоДействияДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_НачалоДействияДоговора");
		ОкончаниеДействияДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_ОкончаниеДействияДоговора");
		Если Дата > ОкончаниеДействияДоговора или Дата < НачалоДействияДоговора Тогда
			ОбщегоНазначения.СообщитьПользователю("Обслуживание может быть создано только на действующий договор!");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
		Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина; 
		
		// проверка на установленный процент
		УсловияОплаты = РегистрыСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(Дата, Новый Структура("Сотрудник",Специалист));
		Если УсловияОплаты.Количество() = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю("Не установлен процент оплаты от работ для данного специалиста");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ЧасРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_ЧасРаботыСпециалиста");
		Для Каждого ТекСтрокаРаботы из ВыполненныеРаботы Цикл
			Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Сотрудник = Специалист;
			Движение.КоличествоЧасов = ТекСтрокаРаботы.ЧасыКОплатеКлиенту;
			Движение.СуммаКОплате = ТекСтрокаРаботы.ЧасыКОплатеКлиенту * ЧасРаботы * УсловияОплаты[0].ПроцентОтРабот / 100;
		КонецЦикла;		
		
		Для Каждого ТекСтрокаРаботы из ВыполненныеРаботы Цикл
			Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Клиент = Клиент;
			Движение.Договор = Договор;
			Движение.КоличествоЧасов = ТекСтрокаРаботы.ЧасыКОплатеКлиенту;
			Движение.СуммаКОплате = ТекСтрокаРаботы.ЧасыКОплатеКлиенту * ЧасРаботы;
		КонецЦикла;		

		
	КонецПроцедуры

#КонецОбласти

#КонецЕсли

// Рагозина -->